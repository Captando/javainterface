import org.gradle.internal.os.OperatingSystem

plugins {
    id 'application'
    id 'org.beryx.jlink' version '3.0.1'
}

group = 'com.captando'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    modularity.inferModulePath = true
}

application {
    mainModule = 'com.captando.calculator'
    mainClass = 'com.captando.calculator.Calculator'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

def currentOs = OperatingSystem.current()

jlink {
    imageName = 'CaptandoCalculator'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']

    launcher {
        name = 'CalculadoraCaptando'
    }

    jpackage {
        appVersion = version.toString()
        vendor = 'Captando'
        description = 'Calculadora Científica com gráficos integrada'
        installerName = 'CaptandoCalculator'
        installerType = project.findProperty('installerType')

        if (currentOs.isWindows()) {
            winMenu = true
            winShortcut = true
            winMenuGroup = 'Captando'
        }
    }
}
